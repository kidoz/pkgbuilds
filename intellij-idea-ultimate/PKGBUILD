# Maintainer: Alexsandr Pavlov <kidoz at mail dot ru>
pkgname=intellij-idea-ultimate
_pkgname=ideaIU
pkgver=2017.1
_pkgver=171.3780.107
pkgrel=1
pkgdesc="Java IDE with the fastest-growing mindshare"
arch=('i686' 'x86_64')
options=('!strip')
url="https://www.jetbrains.com/idea"
license=('custom')
depends=('java-environment')
source=(https://download.jetbrains.com/idea/${_pkgname}-${pkgver}.tar.gz
         jetbrains-idea.desktop)
sha256sums=('4508a4b7b30fb97c02975d72ec5116a3a6fedc2a76758f4087f62cef2b94a8a0'
            '3a0f3804a65fbfb2e6398e4da9129ef934e7898c197e4647ef4a19a90ac810d5')

package() {
  cd "${srcdir}"
  mkdir -p "${pkgdir}/opt/${pkgname}"
  cp -r ${srcdir}/idea-IU-${_pkgver}/* "${pkgdir}/opt/${pkgname}"
  if [[ $CARCH = 'i686' ]]; then
    rm -f "${pkgdir}/opt/${pkgname}/bin/libyjpagent-linux64.so"
    rm -f "${pkgdir}/opt/${pkgname}/bin/libbreakgen64.so"
    rm -f "${pkgdir}/opt/${pkgname}/bin/fsnotifier64"
    rm -f "${pkgdir}/opt/${pkgname}/bin/fsnotifier-arm"
    rm -f "${pkgdir}/opt/${pkgname}/bin/idea64.vmoptions"
  fi
  
  if [[ $CARCH = 'x86_64' ]]; then
    rm -f "${pkgdir}/opt/${pkgname}/bin/libyjpagent-linux.so"
    rm -f "${pkgdir}/opt/${pkgname}/bin/libbreakgen.so"
    rm -f "${pkgdir}/opt/${pkgname}/bin/fsnotifier"
    rm -f "${pkgdir}/opt/${pkgname}/bin/fsnotifier-arm"
    rm -f "${pkgdir}/opt/${pkgname}/bin/idea.vmoptions"
  fi

  mkdir -p "${pkgdir}/usr/share/applications"
  mkdir -p "${pkgdir}/usr/share/pixmaps"
  install -m 644 "${startdir}/jetbrains-idea.desktop" "${pkgdir}/usr/share/applications"
  install -m 644 "${pkgdir}/opt/${pkgname}/bin/idea.png" "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
}

