# Contributor: Ben Ward <benjamin.ward@bathspa.org>
# Maintainer: Alexsandr Pavlov <kidoz at mail dot ru>
pkgname=rstudio-desktop
pkgver=0.98.507
_gwtver=2.5.1
_ginver=1.5
pkgrel=1
pkgdesc="Open source and enterprise-ready professional software for the R community"
arch=('i686' 'x86_64')
url="http://www.rstudio.com/"
license=('AGPL')
depends=('r>=2.11.1' 'qt4' 'qtwebkit' 'boost-libs>=1.50' 'pango' 'shared-mime-info' 'mathjax')
makedepends=('wget' 'cmake>=2.8' 'boost>=1.50' 'java-environment' 'apache-ant' 'openssl' 'pam')
conflicts=('rstudio-desktop-bin' 'rstudio-desktop-git' 'rstudio-desktop-preview-bin')
replaces=('rstudio-desktop-bin' 'rstudio-desktop-git' 'rstudio-desktop-preview-bin')
install=rstudio-desktop.install
changelog=
source=(https://github.com/rstudio/rstudio/tarball/v0.98.507
        https://s3.amazonaws.com/rstudio-buildtools/gin-${_ginver}.zip
        https://s3.amazonaws.com/rstudio-buildtools/gwt-${_gwtver}.zip
        https://s3.amazonaws.com/rstudio-dictionaries/core-dictionaries.zip)
noextract=(gin-${_ginver}.zip
           core-dictionaries.zip)
sha256sums=('1bbe736d8b61df75c99cbb59bfcef375fbc87411d7671ba80780e5599d471fb4'
            'f561f4eb5d5fe1cff95c881e6aed53a86e9f0de8a52863295a8600375f96ab94'
            'c457df45a3eed582452c5784ed11746685df494c0ffe4a8ae4de52945916593a'
            '4341a9630efb9dcf7f215c324136407f3b3d6003e1c96f2e5e1f9f14d5787494')

build() {
  mv ${srcdir}/${pkgname%-desktop}-${pkgname%-desktop}-* ${srcdir}/${pkgname}-${pkgver}
  
  mkdir -p ${srcdir}/${pkgname}-${pkgver}/src/gwt/lib/{gin,gwt} ${srcdir}/${pkgname}-${pkgver}/dependencies/common/dictionaries
  mv ${srcdir}/gwt-${_gwtver} ${srcdir}/${pkgname}-${pkgver}/src/gwt/lib/gwt/${_gwtver}
  unzip -qd ${srcdir}/${pkgname}-${pkgver}/src/gwt/lib/gin/${_ginver} ${srcdir}/gin-${_ginver}.zip
  unzip -qd ${srcdir}/${pkgname}-${pkgver}/dependencies/common/dictionaries ${srcdir}/core-dictionaries.zip
  
  ln -sf /usr/share/mathjax ${srcdir}/${pkgname}-${pkgver}/dependencies/common/mathjax

  cd "${srcdir}/${pkgname}-${pkgver}"

  cmake -DRSTUDIO_TARGET=Desktop \
        -DCMAKE_BUILD_TYPE=Release \
        -DQT_QMAKE_EXECUTABLE=/usr/lib/qt4/bin/qmake \
        -DCMAKE_INSTALL_PREFIX=/usr/lib/rstudio
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}/" install

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/${pkgname}/COPYING"
}
